!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles and input.profiles.extra
!----------------------------------------------------------

subroutine prgen_write

  use prgen_globals
  use EXPRO_interface

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i,ip
  character (len=6) :: iname ! 6 character ion string
  character(len=46) :: header = '#                 IONS :  Name       Z    Mass'
  !---------------------------------------------------------------

  open(unit=1,file='input.profiles',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,'(a,a)') '# input.profiles - Generated by profiles_gen on ',trim(date)
  write(1,'(a)')   '#'
  write(1,'(a)')   '# See https://fusion.gat.com/theory/input.profiles for complete documentation.'
  write(1,'(a)')   '#'
  write(1,'(a)')   '# * Data description:'
  write(1,'(a)')   '#'
  write(1,'(a,a)') '#           INPUT FILE : ',trim(raw_data_file)
  write(1,'(a,i3)')'#    RADIAL GRIDPOINTS : ',nx

  select case (format_type)

  case (1)

     if (cer_file /= "null") then
        write(1,20) '#      MERGED CER FILE : ',trim(cer_file)
     endif
     write(1,40) '#          SHOT NUMBER : ',onetwo_ishot
     write(1,'(a)') '#'
     write(1,'(a)') header

     n_ion = min(onetwo_nion_tot,10)
     do i=1,n_ion
        write(1,'(a,t27,a,t36,i3,t42,f5.1,t48,a)') '#',&
             ion_name(i),&
             ion_z(i),&
             ion_mass(i),&
             ion_type(i)
     enddo

  case (2)

     write(1,20) '#              TOKAMAK : ',trim(plst_tokamak_id)
     write(1,40) '#          SHOT NUMBER : ',plst_shot_number
     write(1,20) '#'
     write(1,'(a)') header

     n_ion = min(plst_dp1_nspec_all-1,10)
     do i=1,n_ion
        write(1,'(a,t27,a,t36,i3,t42,f5.1,t48,a)') '#',&
             ion_name(i),&
             ion_z(i),&
             ion_mass(i),&
             ion_type(i)

     enddo
     write(1,20) '#'
     write(1,'(a,f7.2)') '#       Quasineutrality error (%) ',quasi_err*100.0
     write(1,'(a,f7.2)') '#       e power balance error (%) ',pow_e_err*100.0
     write(1,'(a,f7.2)') '#       i power balance error (%) ',pow_i_err*100.0

  case (3)

     peqdsk_nion = 1+peqdsk_nimp+peqdsk_nbeams

     write(1,'(a)') header

     n_ion = peqdsk_nion
     do i=1,n_ion
        ip = reorder_vec(i)
        call prgen_ion_name(nint(peqdsk_m(ip)),nint(peqdsk_z(ip)),iname)
        write(1,'(a,t27,a,t36,i3,t42,f5.1,t48,a)') '#',&
             iname,&
             nint(peqdsk_z(ip)),&
             peqdsk_m(ip),&
             peqdsk_type(ip)
     enddo

  case (6)

     write(1,20) '#              TOKAMAK : ',ufile_tok
     write(1,20) '#          SHOT NUMBER : ',ufile_shot
     write(1,20) '#             TIME (s) : ',ufile_time

     write(1,'(a)') header

     n_ion = ufile_nion
     do i=1,n_ion
        ip = reorder_vec(i)
        call prgen_ion_name(nint(ufile_m(ip)),nint(ufile_z(ip)),iname)
        write(1,'(a,t27,a,t36,i3,t42,f5.1,t48,a)') '#',&
             iname,&
             nint(ufile_z(ip)),&
             ufile_m(ip),&
             ufile_type(ip)
     enddo

     write(1,20) '#'
     write(1,'(a,f6.2)') '#       Quasineutrality error (%) ',quasi_err*100.0

  case (7)

     if (gmerge_flag == 1) &
          write(1,40) '#          SHOT NUMBER : [DATA MODIFIED WITH GMERGE]'
     write(1,20) '#'
     write(1,'(a)') '#'
     write(1,'(a)') header

     do i=1,n_ion
        ion_name(i) = ion_sanitize(i)
        ion_type(i) = type_therm
        call onetwo_ion_zmass(ion_name(i),ion_z(i),ion_mass(i))
        call prgen_ion_name(nint(ion_mass(i)),ion_z(i),iname)
        write(1,'(a,t27,a,t36,i3,t42,f5.1,t48,a)') '#',&
             iname,&
             ion_z(i),&
             ion_mass(i),&
             ion_type(i)
     enddo

  end select
  write(1,20) '# '
  !---------------------------------------------------------------

  write(1,20) '# * An associated input.profiles.extra was generated'
  write(1,20) '# '

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (efit_method > 1) then
     write(1,20) '# * An associated input.profiles.geo was generated'
     write(1,20) '#'
     if ((format_type == 1 .or. format_type == 2)) then
        if (abs(dpsi_efit/dpsi_data-1) > 0.001) then
           write(1,'(a,1pe9.2,a)') &
                '#    FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [WARNING]'
        else
           write(1,'(a,1pe9.2,a)') '#   FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [GOOD]'
        endif
     endif
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#                NSURF : ',nsurf
     write(1,40) '#                 NARC : ',narc
     if (efit_method /= 4) then
        write(1,20) '#            EFIT HEADER ... '
        write(1,20) '#'
        write(1,20) '#',efit_header
     endif
     write(1,20) '#'
  endif
  !---------------------------------------------------------------

  !---------------------------------------------------------------
  ! Merged files and extra information
  !
  write(1,20) '# * Plasma dimensions:'
  write(1,20) '#'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MAJOR RADIUS : ',rmaj(nx),' m'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MINOR RADIUS : ',rmin(nx),' m'
  write(1,20) '#'
  write(1,20)    '# * Field/current orientation:'
  write(1,'(a)') '# '
  write(1,'(a)') '#   NOTE: All variables carry correct and consistent signs'
  write(1,'(a)') '#         in fixed gacode (r,theta,varphi) coordinates.'
  write(1,20) '#'
  write(1,'(a,i2)')       '#                IPCCW : ',ipccw
  write(1,'(a,i2)')       '#                BTCCW : ',btccw
  !---------------------------------------------------------------

  EXPRO_n_exp = nx

  ! COORDINATES: For all data sources, ensure correct sign of EXPRO_b_ref
  !              (and thus toroidal flux)
  !
  select case (format_type)

  case (0)
     EXPRO_b_ref = -btccw*abs(null_bref)
     EXPRO_arho  = null_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (1)
     EXPRO_b_ref = -btccw*abs(onetwo_Btor)
     EXPRO_arho  = onetwo_rho_grid(nx)
     EXPRO_rvbv  = onetwo_R0*onetwo_Btor
     EXPRO_ip_exp = ip_tot
  case (2)
     EXPRO_b_ref = -btccw*abs(plst_b_axis_vac)
     EXPRO_arho  = sqrt(plst_phit(nx)/plst_b_axis_vac/pi)
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (3)
     EXPRO_b_ref = -btccw*abs(peqdsk_bref)
     EXPRO_arho  = peqdsk_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (5)
     EXPRO_b_ref = -btccw*abs(corsica_bref)
     EXPRO_arho  = corsica_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (6)
     EXPRO_b_ref = -btccw*abs(ufile_bref)
     EXPRO_arho  = ufile_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  end select

  write(1,20) '# '
  if (n_ion < 10) then
     write(1,24) 'N_ION=',n_ion
  else
     write(1,25) 'N_ION=',n_ion
  endif

  if (EXPRO_n_exp > 99) then
     write(1,30) 'N_EXP=',EXPRO_n_exp
  else
     write(1,25) 'N_EXP=',EXPRO_n_exp
  endif
  write(1,'(a,sp,1pe14.7)') 'BT_EXP=',EXPRO_b_ref
  write(1,60) 'IP_EXP=',EXPRO_ip_exp
  write(1,60) 'RVBV=',abs(EXPRO_rvbv)
  write(1,60) 'ARHO_EXP=',EXPRO_arho

  !-------------------------------------------------------------------------------------
  ! Now we need to write the profile data
  !
  call EXPRO_write(1)
  close(1)
  print '(a)','INFO: (prgen) Wrote input.profiles.'
  !-------------------------------------------------------------------------------------

  !-----------------------------------------------------------
  ! Set EXPRO ctrl parameters for derived parameter calc.
  !
  EXPRO_n_ion = n_ion ! Need to set this since we aren't EXPRO_pread-ing it
  EXPRO_ctrl_n_ion = n_ion
  EXPRO_ctrl_quasineutral_flag = 0
  EXPRO_ctrl_z(1) = 1.0
  if (efit_method > 1) then
     EXPRO_ctrl_numeq_flag = 1
     call EXPRO_read_geo
  else
     EXPRO_ctrl_numeq_flag = 0
  endif
  !
  call EXPRO_compute_derived
  call EXPRO_write_derived(1,'input.profiles.extra')
  print '(a)','INFO: (prgen) Wrote input.profiles.extra.'
  !-----------------------------------------------------------------------------------

20 format(5(a))
24 format(a,i1)
25 format(a,i2)
30 format(a,i3)
40 format(a,i6)
60 format(a,1pe13.7)

end subroutine prgen_write
